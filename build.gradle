plugins {
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '6.4.0'
    id "io.freefair.lombok" version "5.0.1"
    id "java-library"
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

archivesBaseName = 'amazon-chime-voiceconnector-recordandtranscribe'
repositories {
    mavenCentral()
}

java {
    toolchain {
        // By default, Gradle uses the same Java version for running Gradle itself and building JVM projects.
        // It causes FUNCTION_ERROR_INIT_FAILURE when launching Lambda due to UnsupportedClassVersionError
        // To match Java runtime version from Lambda, here we explicitly require gradle to compile with Jdk
        // Gradle document: https://docs.gradle.org/current/userguide/toolchains.html
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.2'
    compile(
            'software.amazon.awssdk:transcribestreaming:2.17.271',

            'com.amazonaws:aws-java-sdk-dynamodb:1.12.300',
            'com.amazonaws:aws-java-sdk-kinesisvideo:1.12.300',
            'com.amazonaws:aws-lambda-java-core:1.2.1',
            'com.amazonaws:aws-lambda-java-events:3.11.0',
            'com.amazonaws:aws-java-sdk-cloudwatch:1.12.300',
            'com.amazonaws:aws-java-sdk-apigatewaymanagementapi:1.12.300',
            'com.amazonaws:aws-java-sdk-ecs:1.12.300',
            'com.amazonaws:amazon-kinesis-video-streams-parser-library:1.2.4',
            'org.slf4j:slf4j-api:2.0.1',
            'org.slf4j:slf4j-reload4j:2.0.1',
            'javax.xml.bind:jaxb-api:2.3.1',

            // need this for our async clients
            'software.amazon.awssdk:netty-nio-client:2.17.271',

            // need this for logging
            'org.apache.commons:commons-lang3:3.12.0',

            // need for argument parsing
            'commons-cli:commons-cli:1.5.0'
    )
}

task dockerJar(type: Jar) {
    manifest {
        attributes 'Main-Classs': 'com.amazonaws.kvstranscribestreaming.KVSTranscribeStreamingDocker'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyJarAndNoticesToDocker(type: Copy) {
    from dockerJar, "$projectDir/NOTICES"
    into "$buildDir/docker"
}

task buildDockerFile(type: Dockerfile) {
    from("amazoncorretto:8")
    workingDir("/tmp/chime-streaming-transcribe/")
    addFile(archivesBaseName + ".jar", "./lib/")
    addFile("NOTICES", "./")
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn(copyJarAndNoticesToDocker, buildDockerFile)
    images.add('chime-transcribe:latest')
}

task buildZip(type: Zip) {
    from compileJava
    from processResources

    into('lib') {
        from configurations.compileClasspath
    }
}

task copyZipFile(type: Copy) {
    dependsOn tasks.named("buildZip")
    from "$buildDir/distributions/amazon-chime-voiceconnector-recordandtranscribe.zip"
    into "./infrastructure"
}

task copyCFTemplate(type: Copy){
    from "infrastructure/deployment-template.json"
    into "$buildDir/distributions"
}

task zipAndCopyRunTaskFunction(type: Zip){
    from "infrastructure/sendRunTaskRequestLambdaFunction.js"
    archiveFileName = "send-run-task-request-function.zip"
    destinationDirectory = file("$buildDir/distributions")
}

build.dependsOn buildZip, copyZipFile, copyCFTemplate, zipAndCopyRunTaskFunction, buildDockerFile
